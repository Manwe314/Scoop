#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive    : enable

layout (set = 1, binding = 6) uniform FsrConstBuffer {
    uvec4 Const0;
    uvec4 Const1;
    uvec4 Const2;
    uvec4 Const3;
    uvec4 conSharp;
} fsr;

#define A_GPU  1
#define A_GLSL 1

#define SAMPLE_SLOW_FALLBACK 1
#define SAMPLE_RCAS          1

#include "../external/ffx_a.h"

layout(set = 1, binding = 8) uniform sampler2D UpscaledTexture;

layout(set = 1, binding = 5, rgba16f) uniform writeonly image2D OutputTexture;

#define FSR_RCAS_F 1

AF4 FsrRcasLoadF(ASU2 p)
{
    ivec2 ip = ivec2(p);
    vec2  dim = vec2(textureSize(UpscaledTexture, 0));
    vec2  uv = (vec2(ip) + 0.5) / dim;

    return texture(UpscaledTexture, uv);
}

void FsrRcasInputF(inout AF1 r, inout AF1 g, inout AF1 b) {}

#include "../external/ffx_fsr1.h"

void CurrFilter(AU2 pos)
{
    AF1 r, g, b;
    FsrRcasF(r, g, b, pos, fsr.conSharp);

    imageStore(OutputTexture, ivec2(pos), vec4(r, g, b, 1.0));
}

layout(local_size_x = 64, local_size_y = 1) in;
void main()
{
    AU2 gxy = ARmp8x8(gl_LocalInvocationID.x) + AU2(gl_WorkGroupID.x << 4u, gl_WorkGroupID.y << 4u);

    CurrFilter(gxy);
    gxy.x += 8u;
    CurrFilter(gxy);
    gxy.y += 8u;
    CurrFilter(gxy);
    gxy.x -= 8u;
    CurrFilter(gxy);
}
