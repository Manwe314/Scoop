#version 460
layout(local_size_x = 8, local_size_y = 8) in;

layout(set = 1, binding = 4, rgba16f) uniform image2D outImage;
layout(set = 1, binding = 14) uniform sampler2D nrdOutDiff;
layout(set = 1, binding = 15) uniform sampler2D nrdOutSpec;


layout(set = 1, binding = 9,  rgba16f) uniform readonly image2D inDiff;
layout(set = 1, binding = 10, rgba16f) uniform readonly image2D inSpec;
layout(set = 1, binding = 11, rgba16_snorm) uniform readonly image2D inNormalRoughness;
layout(set = 1, binding = 12, r32f)         uniform readonly image2D inViewZ;
layout(set = 1, binding = 13, rgba16f)   uniform readonly image2D inMotion;
layout(set = 1, binding = 18) uniform sampler2D nrdValidation;

layout(set = 1, binding = 3, std430) readonly buffer ParamsBuf {
    mat4  viewProjInv;
    vec4  camPos_time;
    uvec2 imageSize;
    uint  rootIndex;
    uint  flags;
    vec2  cameraJitter;
    vec2  cameraJitterPrev;
} Params;

const uint FLAG_DEBUG_NRD_INPUTS = 1u << 19;

const float NRD_FP16_MAX = 65504.0;

bool isBad(float v)    { return isnan(v) || isinf(v); }
bool isBad(vec3 v)     { return any(isnan(v)) || any(isinf(v)); }

vec3 sanitizeVec3(vec3 v, float clampLen)
{
    if (isBad(v))
        return vec3(0.0);
    return clamp(v, vec3(-clampLen), vec3(clampLen));
}

vec3 sanitizeNormal(vec3 n)
{
    if (isBad(n) || length(n) < 1e-6)
        return vec3(0.0, 0.0, 1.0);
    return normalize(n);
}

float sanitizeViewZ(float z)
{
    if (isBad(z))
        return 0.0;
    return clamp(z, 0.0, NRD_FP16_MAX);
}

vec3 visualizeViewZ(float z)
{
    // Log-ish scale to bring large depths into [0,1].
    float v = log2(max(z, 1e-4) + 1.0) * 0.05;
    return vec3(clamp(v, 0.0, 1.0));
}

vec3 visualizeMotion(vec2 mv)
{
    float mag = length(mv);
    return vec3(mv * 0.5 + 0.5, clamp(mag * 0.1, 0.0, 1.0));
}

vec3 fetchInput(uint idx, ivec2 samplePx)
{
    if (idx == 1u) return sanitizeVec3(imageLoad(inDiff, samplePx).xyz, NRD_FP16_MAX);
    if (idx == 0u) return sanitizeVec3(imageLoad(inSpec, samplePx).xyz, NRD_FP16_MAX);
    if (idx == 2u)
    {
        vec4 nr = imageLoad(inNormalRoughness, samplePx);
        vec3 n  = sanitizeNormal(nr.xyz);
        return n * 0.5 + 0.5;
    }
    if (idx == 3u)
    {
        float z = sanitizeViewZ(imageLoad(inViewZ, samplePx).x);
        return visualizeViewZ(z);
    }
    if (idx == 4u)
    {
        vec2 mv = sanitizeVec3(imageLoad(inMotion, samplePx).xyz, NRD_FP16_MAX).xy;
        return visualizeMotion(mv);
    }
    return vec3(0.0);
}

void main()
{
    uvec2 gid = gl_GlobalInvocationID.xy;
    if (gid.x >= Params.imageSize.x || gid.y >= Params.imageSize.y)
        return;

    ivec2 pixel = ivec2(gid);
    bool debugInputs = (Params.flags & FLAG_DEBUG_NRD_INPUTS) != 0u;

    vec3 color;
    if (!debugInputs)
    {
        vec3 diff = texelFetch(nrdOutDiff, pixel, 0).xyz;
        vec3 spec = texelFetch(nrdOutSpec, pixel, 0).xyz;
        color = diff + spec;
    }
    else
    {
        color = texelFetch(nrdValidation, pixel, 0).xyz;
    }

    imageStore(outImage, pixel, vec4(color, 1.0));
}
